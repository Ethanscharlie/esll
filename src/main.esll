" --------------- Constants --------------- "
Decimal SCREEN_WIDTH 800
Decimal SCREEN_HEIGHT 800
Decimal SPEED 10
Decimal GRAVITY 0.35
Decimal JUMP_POWER 70

" --------------- box variables --------------- "
Decimal boxX 100
Decimal boxY 100
Decimal boxW 100
Decimal boxH 100
Decimal boxVelocityY 0

Void start()
end

Void draw()
  setBackground(100, 200, 100)

  " --------------- Update Box Phyics --------------- "
  boxVelocityY boxVelocityY + GRAVITY * deltatime

  if pressingKey(">")
    boxX boxX + SPEED * deltatime
  end
  if pressingKey("<")
    boxX boxX - SPEED * deltatime
  end
  if pressingKey(" ")
    boxVelocityY -JUMP_POWER * deltatime
  end

  boxY boxY + boxVelocityY * deltatime

  if boxY + boxH > SCREEN_HEIGHT
    boxY SCREEN_HEIGHT - boxH
    boxVelocityY 0
  end

  " --------------- Draw Box --------------- "
  fill(255, 0, 0)
  drawRectangle(boxX, boxY, boxW, boxH)
end

